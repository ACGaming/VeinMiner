
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'curseforge'

ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    //noinspection GrUnresolvedAccess
    ext.config = new ConfigSlurper().parse prop
}

group= "portablejim.veinminer"
version = config.mod_version
archivesBaseName = "VeinMiner"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    mappings = "snapshot_20150220"
}

String buildNumber = "unknown"
if(System.getenv("BUILD_NUMBER") != null) {
    buildNumber = System.getenv("BUILD_NUMBER")
}

version = "${config.minecraft_version}_${config.mod_version}.${buildNumber}"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'version.properties'

        // replace version and mcversion
        expand 'version':project.config.mod_version, 'mcversion':project.config.minecraft_version, 'buildnumber': buildNumber
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'version.properties'
    }
}

ext.sharedmanifest = manifest {
        //attributes 'FMLCorePlugin': 'portablejim.veinminer.asm.VeinMinerCorePlugin'
        //attributes 'FMLCorePluginContainsFMLMod': 'true'
}

jar {
    manifest = project.manifest {
        from sharedmanifest
    }
}

sourceCompatibility = 6
targetCompatibility = 6

task apiJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'api'
    include "portablejim/veinminer/api/**"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    manifest = project.manifest {
        from sharedmanifest
    }
    classifier = 'dev'
}

//noinspection GrUnresolvedAccess
task release(type: Copy) {
    dependsOn "build"

    from project.tasks.jar.archivePath
    into "release/"
}

runClient {
    jvmArgs "-Dfml.coreMods.load=portablejim.veinminer.asm.VeinMinerCorePlugin"
}

build.dependsOn apiJar, deobfJar

artifacts {
    archives deobfJar
}

configurations {
    deployerJars
}

uploadArchives {
    dependsOn 'reobf'

    repositories {
        if(project.hasProperty("pushMaven")) {
            logger.info("Pushing to maven repo")

            mavenDeployer {
                configuration = configurations.deployerJars

                repository(url: "file://localhost/home/public_html/maven.jamesmckay.id.au/htdocs/")

                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    packaging = 'jar'
                    description 'VeinMiner Mod'
                }
            }
        }
    }
}

ext.curseForgeReleaseType = null

task ('curseGeneral') << {
    curse {
        projectId = '67133'
        if (project.hasProperty('curseForgeApiKey')) apiKey = project.curseForgeApiKey
        else apiKey = 0
        releaseType = curseForgeReleaseType
        changelog = new File('Changelog.md').getText('utf-8').split('\n-{3,8}|\n\n')[2]
        displayName = String.format('VeinMiner %s [MC%s; build %s]', config.mod_version, config.compatible_versions, buildNumber)
        additionalArtifact deobfJar, apiJar

        for (String newVersion : config.compatible_versions.split(",")) {
            addGameVersion newVersion
        }
    }.execute()
}

task setCurseAlpha << {
    print('Set alpha')
    curseForgeReleaseType = 'alpha'
}

task setCurseRelease << {
    print('Set release')
    curseForgeReleaseType = 'release'
}

task curseAlpha(dependsOn: 'curseGeneral') {
    curseGeneral.dependsOn setCurseAlpha
}

task curseRelease(dependsOn: 'curseGeneral') {
    curseGeneral.dependsOn setCurseRelease

}


